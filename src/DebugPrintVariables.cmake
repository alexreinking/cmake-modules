function(cmrk_debug_print_variables)
    get_directory_property(_cmrk_variables VARIABLES)
    get_directory_property(_cmrk_cache_variables CACHE_VARIABLES)
    list(SORT _cmrk_variables)
    list(SORT _cmrk_cache_variables)

    set(_cmrk_shadow_variables "")
    foreach (_cmrk_var IN LISTS _cmrk_cache_variables)
        list(REMOVE_ITEM _cmrk_variables "${_cmrk_var}")
        if (NOT "$CACHE{${_cmrk_var}}" STREQUAL "${${_cmrk_var}}")
            list(APPEND _cmrk_shadow_variables "${_cmrk_var}")
        endif ()
    endforeach ()

    cmake_parse_arguments(PARSE_ARGV 0 _cmrk "NO_VALUES" "INCLUDE_REGEX;EXCLUDE_REGEX" "")

    if (NOT DEFINED _cmrk_INCLUDE_REGEX)
        set(_cmrk_INCLUDE_REGEX ".*")
    endif ()

    if (NOT DEFINED _cmrk_EXCLUDE_REGEX)
        set(_cmrk_EXCLUDE_REGEX "^$")
    endif ()

    message(STATUS "Cache variables:")
    foreach (_cmrk_var IN LISTS _cmrk_cache_variables)
        if (NOT _cmrk_var MATCHES "${_cmrk_INCLUDE_REGEX}")
            continue()
        endif ()

        if (_cmrk_var MATCHES "${_cmrk_EXCLUDE_REGEX}")
            continue()
        endif ()

        get_property(_cmrk_type CACHE "${_cmrk_var}" PROPERTY TYPE)
        if (_cmrk_NO_VALUES)
            message(STATUS "${_cmrk_var}:${_cmrk_type}")
        else ()
            get_property(_cmrk_val CACHE "${_cmrk_var}" PROPERTY VALUE)
            message(STATUS "${_cmrk_var}:${_cmrk_type} = ${_cmrk_val}")
        endif ()
    endforeach ()
    message(STATUS "")

    message(STATUS "Normal variables:")
    foreach (_cmrk_var IN LISTS _cmrk_variables)
        if (NOT _cmrk_var MATCHES "${_cmrk_INCLUDE_REGEX}")
            continue()
        endif ()

        if (_cmrk_var MATCHES "${_cmrk_EXCLUDE_REGEX}")
            continue()
        endif ()

        if (_cmrk_NO_VALUES)
            message(STATUS "${_cmrk_var}")
        else ()
            message(STATUS "${_cmrk_var} = ${${_cmrk_var}}")
        endif ()
    endforeach ()
    message(STATUS "")

    message(STATUS "Normal variables shadowing cache entries:")
    foreach (_cmrk_var IN LISTS _cmrk_shadow_variables)
        if (NOT _cmrk_var MATCHES "${_cmrk_INCLUDE_REGEX}")
            continue()
        endif ()

        if (_cmrk_var MATCHES "${_cmrk_EXCLUDE_REGEX}")
            continue()
        endif ()

        if (_cmrk_NO_VALUES)
            message(STATUS "${_cmrk_var}")
        else ()
            message(STATUS "${_cmrk_var} = ${${_cmrk_var}}")
        endif ()
    endforeach ()
    message(STATUS "")
endfunction()
